IF OBJECT_ID (N'dbo.up_front_GetGoodsInfo', N'P') IS NOT NULL DROP PROCEDURE dbo.up_front_GetGoodsInfo
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

/*
	작성정보   : [2003:07:22    12:35]  JJH: 
	관련페이지 : wedd_det.asp
	내용	   : 카드상품 상세정보
	
	수정정보   : 
*/
CREATE Procedure  [dbo].[up_front_GetGoodsInfo]
	@CARD_SEQ			varchar(10)
,	@CARD_CATEGORY_SEQ		varchar(10)
,	@CARD_COUNT			int	=400
as
	DECLARE @DISCOUNT_RATE  		float
		,@CARD_PRICE_CUSTOMER	int   --  소비자가
		,@SELL_PRICE			int	-- 판매정가 (400장을 기준으로 할인율이 적용된가격)
	SET @SELL_PRICE = 0
	SET @DISCOUNT_RATE =0
-- 카드 시중가 산출	
	SELECT  @CARD_PRICE_CUSTOMER = CD.CARD_PRICE_CUSTOMER FROM dbo.card CD WHERE CD.CARD_SEQ = @CARD_SEQ
-- 카드 카테고리 수량, 시중가를 이용한 할인율 산출
	SELECT TOP 1 @DISCOUNT_RATE = DISCOUNT_RATE FROM dbo.DISCOUNT_POLICY  
					WHERE  	CARD_CATEGORY_SEQ=@CARD_CATEGORY_SEQ  
					AND 	MIN_PRICE <=  @CARD_PRICE_CUSTOMER
					AND	MAX_PRICE >= @CARD_PRICE_CUSTOMER
					AND	MIN_COUNT<= 	@CARD_COUNT
					AND 	MAX_COUNT>=	@CARD_COUNT
	
	
	IF ( ISNULL(@DISCOUNT_RATE,0) = 0)	-- 시중가를 산출한것이 NULL 이거나 0이면 상위 카테고리를 이용해서 한번더 검색한다
						-- TABLE 구조가 합리적으로 되어 있지않아서 JOIN 문장이 복잡해진다.
	BEGIN
		DECLARE	 @CATEGORY_UPPER_CODE	INT
		SELECT @CATEGORY_UPPER_CODE = CC.CATEGORY_UPPER_CODE FROM dbo.card_category CC	WHERE CC.CARD_CATEGORY_SEQ= @CARD_CATEGORY_SEQ
		-- 상위 코드가 있다면 상위코드값을 이용해서 다시 할인율을 구해본다
		IF (ISNULL(@CATEGORY_UPPER_CODE,0) != 0)	
			BEGIN
				SELECT TOP 1 @DISCOUNT_RATE = DISCOUNT_RATE FROM dbo.DISCOUNT_POLICY  
					WHERE  	CARD_CATEGORY_SEQ=@CATEGORY_UPPER_CODE  
					AND 	MIN_PRICE <=  @CARD_PRICE_CUSTOMER
					AND	MAX_PRICE >= @CARD_PRICE_CUSTOMER
					AND	MIN_COUNT<= 	@CARD_COUNT
					AND 	MAX_COUNT>=	@CARD_COUNT
			END
	END
-- 할인율을 구했다면
	IF ( @DISCOUNT_RATE !=0)
	BEGIN
		SET @SELL_PRICE = @CARD_PRICE_CUSTOMER*((100-@DISCOUNT_RATE)/100)
	END
		
-- 계산한값을 SELECT
	SELECT CD.*
		,@DISCOUNT_RATE 	AS DISCOUNT_RATE
		,@SELL_PRICE		AS SELL_PRICE
		, ISNULL((SELECT TOP 1 F_CD.CARD_SEQ FROM dbo.card F_CD WHERE F_CD.CARD_CATEGORY_SEQ=@CARD_CATEGORY_SEQ AND F_CD.CARD_SEQ < @CARD_SEQ  ORDER BY F_CD.CARD_SEQ DESC),@CARD_SEQ)  as B_CARD_REQ
		,  ISNULL((SELECT TOP 1 F_CD.CARD_SEQ FROM dbo.card F_CD WHERE F_CD.CARD_CATEGORY_SEQ=@CARD_CATEGORY_SEQ AND F_CD.CARD_SEQ > @CARD_SEQ  ORDER BY F_CD.CARD_SEQ ),@CARD_SEQ) as  A_CARD_REQ
	FROM dbo.card CD WHERE CD.CARD_SEQ  = @CARD_SEQ

GO
