IF OBJECT_ID (N'dbo.SP_EXEC_COUPON_ISSUE_FOR_AUTO', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_EXEC_COUPON_ISSUE_FOR_AUTO
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*

EXEC SP_EXEC_COUPON_ISSUE_FOR_AUTO 5001, 's4guest', 6

*/
CREATE PROCEDURE [dbo].[SP_EXEC_COUPON_ISSUE_FOR_AUTO]
		@COMPANY_SEQ			AS INT
	,   @USER_ID				AS VARCHAR(50)
	,   @CMMN_CODE				AS VARCHAR(6)
	,	@ORDER_SEQ				AS INT
AS
BEGIN
    
    SET NOCOUNT ON;

	DECLARE @T_CD TABLE
    (
			COUPON_DETAIL_SEQ	INT
		,	COUPON_CNT			INT
		,	EXPIRY_CUSTOM_VALUE INT NULL
	)

	DECLARE @CARD_SEQ					AS INT
		,	@CARD_ORDER_CNT				AS INT
		,	@ORDER_AMT					AS INT
		,	@REFERER_SALES_GUBUN		AS VARCHAR(10)
		,	@USER_REG_DATE				AS DATETIME
		,	@WEDDING_DATE				AS DATETIME
		,	@WEDD_PLACE					AS NVARCHAR(300)
		,	@ORG_ORDER_YORN				AS VARCHAR(1)



	/* 주문내역 가져오기 */
	SELECT	@CARD_SEQ		= CO.CARD_SEQ
		,	@CARD_ORDER_CNT = CO.ORDER_COUNT
		,	@ORDER_AMT		= CO.LAST_TOTAL_PRICE
		,	@WEDD_PLACE		= ISNULL(CASE WHEN COW.WEDD_ADDR IS NULL THEN '' ELSE LEFT(COW.WEDD_ADDR, 2) END, '')
		,	@ORG_ORDER_YORN = CASE WHEN CO.UP_ORDER_SEQ IS NULL THEN 'Y' ELSE 'N' END

	FROM	CUSTOM_ORDER CO
	LEFT 
	JOIN	CUSTOM_ORDER_WEDDINFO COW ON CO.ORDER_SEQ = COW.ORDER_SEQ
	WHERE	CO.ORDER_SEQ = @ORDER_SEQ
	AND		CO.MEMBER_ID = @USER_ID



	/* 회원정보 가져오기 */
	SELECT	@REFERER_SALES_GUBUN		= CASE WHEN ISNULL(SUT.REFERER_SALES_GUBUN, SUT.SELECT_SALES_GUBUN) = '' THEN 'SB' ELSE ISNULL(SUT.REFERER_SALES_GUBUN, SUT.SELECT_SALES_GUBUN) END
		,	@USER_REG_DATE				= SUT.INTERGRATION_DATE
		,	@WEDDING_DATE				= CASE WHEN ISDATE(SUT.WEDD_YEAR + '-' + SUT.WEDD_MONTH + '-' + SUT.WEDD_DAY) = 1 THEN SUT.WEDD_YEAR + '-' + SUT.WEDD_MONTH + '-' + SUT.WEDD_DAY ELSE NULL END

	FROM	S2_USERINFO_THECARD SUT
	WHERE	SUT.UID = @USER_ID



	/* 해당되는 쿠폰 가져오기 */
	INSERT INTO @T_CD (COUPON_DETAIL_SEQ, COUPON_CNT, EXPIRY_CUSTOM_VALUE)
	SELECT	MAX(CD.COUPON_DETAIL_SEQ)
		,	COUNT(*)
		,	MAX(CM.EXPIRY_CUSTOM_VALUE)

	FROM	COUPON_MST CM

	JOIN	COUPON_DETAIL CD 
		ON		CM.COUPON_MST_SEQ			= CD.COUPON_MST_SEQ 
		AND		CD.DOWNLOAD_ACTIVE_YN		= 'Y'

	/* 대상 사이트의 쿠폰만 가져옴 */
	JOIN	COUPON_APPLY_SITE CAS 
		ON		CM.COUPON_MST_SEQ			= CAS.COUPON_MST_SEQ 
		AND		CAS.COMPANY_SEQ				= @COMPANY_SEQ
	
	WHERE	1 = 1

	AND		CM.STATUS_ACTIVE_YN = 'Y'

	/* 자동발급 프로세스 */
	/* M : 수동, A : 자동, E : 기타 */
	AND		CM.DOWNLOAD_KIND IN ('A', 'E')
	AND		CM.DOWNLOAD_KIND_ETC_CODE = @CMMN_CODE

	/* 발행기간 */
	AND		(COUPON_ISSUE_START_DATE	IS NULL OR COUPON_ISSUE_START_DATE	<= GETDATE())
	AND		(COUPON_ISSUE_END_DATE		IS NULL OR COUPON_ISSUE_END_DATE	>= GETDATE())

	/* 가입 사이트 */
	AND		(CM.REFERER_SALES_GUBUN = '' OR CM.REFERER_SALES_GUBUN LIKE '%' + @REFERER_SALES_GUBUN + '%')
	
	/* 가입일 */
	AND		(		(CM.USER_REG_START_DATE IS NULL OR CM.USER_REG_START_DATE	<= @USER_REG_DATE)
				AND	(CM.USER_REG_END_DATE	IS NULL OR CM.USER_REG_END_DATE		>= @USER_REG_DATE)
			)

	/* 결혼예정일 */
	AND		(		(CM.WEDDING_START_DATE	IS NULL OR CM.WEDDING_START_DATE	<= @WEDDING_DATE)
				AND	(CM.WEDDING_END_DATE	IS NULL OR CM.WEDDING_END_DATE		>= @WEDDING_DATE)
			)

	/* 예식 지역 */
	AND		(CM.WEDD_PLACE = '' OR CM.WEDD_PLACE LIKE '%' + @WEDD_PLACE + '%')

	/* 샘플 주문 여부 */
	AND		(
					CM.SAMPLE_ORDER_TYPE = 'ALL'

				OR	(
							CM.SAMPLE_ORDER_TYPE = 'NO'
						AND	ISNULL	(	
										(
											SELECT	COUNT(*) 
											FROM	CUSTOM_SAMPLE_ORDER 
											WHERE	MEMBER_ID = @USER_ID 
											AND		STATUS_SEQ >= 4
											AND		COMPANY_SEQ = @COMPANY_SEQ
										)
									, 0) = 0		
					)

				OR	(
							CM.SAMPLE_ORDER_TYPE = 'YES'
						AND	ISNULL	(	
										(
											SELECT	COUNT(*) 
											FROM	CUSTOM_SAMPLE_ORDER 
											WHERE	MEMBER_ID = @USER_ID 
											AND		STATUS_SEQ >= 4
											AND		COMPANY_SEQ = @COMPANY_SEQ
											AND		(CM.SAMPLE_ORDER_START_DATE IS NULL OR REQUEST_DATE >= CM.SAMPLE_ORDER_START_DATE)
											AND		(CM.SAMPLE_ORDER_END_DATE	IS NULL OR REQUEST_DATE <= CM.SAMPLE_ORDER_END_DATE)
										)
									, 0) > 0		
					)
			)
		
	/* 청첩장 주문 여부 */
	AND		(
					CM.CARD_ORDER_TYPE = 'ALL'

				OR	(
							CM.CARD_ORDER_TYPE = 'NO'
						AND	ISNULL	(	
										(
											SELECT	COUNT(*) 
											FROM	CUSTOM_ORDER 
											WHERE	MEMBER_ID = @USER_ID 
											AND		SETTLE_STATUS = 2
											AND		COMPANY_SEQ = @COMPANY_SEQ
										)
									, 0) = 0
					)

				OR	(
							CM.CARD_ORDER_TYPE = 'YES'
						AND	ISNULL	(	
										(
											SELECT	COUNT(*) 
											FROM	CUSTOM_ORDER 
											WHERE	MEMBER_ID = @USER_ID 
											AND		SETTLE_STATUS = 2
											AND		COMPANY_SEQ = @COMPANY_SEQ
											AND		(CM.CARD_ORDER_CNT = 0 OR ORDER_COUNT >= CM.CARD_ORDER_CNT)
										)
									, 0) > 0		
					)
			)

	/* 발급 제한 */
	AND		(
				CM.DOWNLOAD_LIMIT_PER_PERSON_QTY 
				> 
				ISNULL	(	
							(
								SELECT	COUNT(*) 
								FROM	COUPON_ISSUE S_CI
								JOIN	COUPON_DETAIL S_CD ON S_CI.COUPON_DETAIL_SEQ = S_CD.COUPON_DETAIL_SEQ
								WHERE	1 = 1
								AND		S_CD.COUPON_MST_SEQ = CM.COUPON_MST_SEQ 
								AND		S_CI.UID = @USER_ID
								AND		S_CI.COMPANY_SEQ = @COMPANY_SEQ
							)
						, 0)
			)
	
	GROUP BY CM.COUPON_MST_SEQ



	/* 지급해야될 쿠폰이 있다면 */
	IF EXISTS(SELECT * FROM @T_CD)
	BEGIN
		


		/* 쿠폰 지급 */
		INSERT INTO COUPON_ISSUE (COUPON_DETAIL_SEQ, UID, ACTIVE_YN, COMPANY_SEQ, SALES_GUBUN, END_DATE)
		SELECT	COUPON_DETAIL_SEQ
			,	@USER_ID
			,	'Y'
			,	@COMPANY_SEQ
			,	CASE 
						WHEN @COMPANY_SEQ = 5001 THEN 'SB'
						WHEN @COMPANY_SEQ = 5003 THEN 'SS'
						WHEN @COMPANY_SEQ = 5006 THEN 'SA'
						WHEN @COMPANY_SEQ = 5007 THEN 'ST'
						WHEN @COMPANY_SEQ = 5000 THEN 'B'
						ELSE 'B'
				END
			,	CASE WHEN EXPIRY_CUSTOM_VALUE IS NULL THEN NULL ELSE (CONVERT(VARCHAR(10), DATEADD(DAY, EXPIRY_CUSTOM_VALUE ,GETDATE()), 120) + ' 23:59:59.997') END
		FROM	@T_CD



		/* 쿠폰 다운로드 카운트를 증가 */
		UPDATE	COUPON_MST
		SET		DOWNLOADED_CNT = DOWNLOADED_CNT + 1
		WHERE	COUPON_MST_SEQ IN	( 
										SELECT	CD.COUPON_MST_SEQ
										FROM	@T_CD T_CD
										JOIN	COUPON_DETAIL CD ON T_CD.COUPON_DETAIL_SEQ = CD.COUPON_DETAIL_SEQ
										WHERE	T_CD.COUPON_CNT > 1 
									)



		/* 1번만 사용할 수 있는 쿠폰 이라면, 사용완료로 업데이트 한다. */
		UPDATE	COUPON_DETAIL
		SET		DOWNLOAD_ACTIVE_YN = 'N'
		WHERE	COUPON_DETAIL_SEQ IN ( SELECT COUPON_DETAIL_SEQ FROM @T_CD WHERE COUPON_CNT > 1 )

	END



END
GO
