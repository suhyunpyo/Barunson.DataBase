IF OBJECT_ID (N'dbo.UP_INSERT_S4_EVENTMUSIC_REPLY_NEWCARD', N'P') IS NOT NULL DROP PROCEDURE dbo.UP_INSERT_S4_EVENTMUSIC_REPLY_NEWCARD
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- AUTHOR:		표수현
-- CREATE DATE: 2022-07-19
-- DESCRIPTION:	프론트, 초대 이벤트 참여, S4_EVENTMUSIC_REPLY_NEW, 모바일 체킹 가능

-- EXEC UP_INSERT_S4_EVENTMUSIC_REPLY 86, 'BHANDSTEST', 5006, 'M'
-- EXEC UP_INSERT_S4_EVENTMUSIC_REPLY 87, 'BHANDSTEST', 5006, 'M'

-- =============================================
/*

EXEC UP_INSERT_S4_EVENTMUSIC_REPLY 104, 'S4GUEST', 5001, 'ASD'

*/
CREATE PROC [dbo].[UP_INSERT_S4_EVENTMUSIC_REPLY_NEWCARD]
 @EVENT_SEQ		INT, 
 @UID			VARCHAR(100), 
 @COMPANY_SEQ	INT,
 @COMMENT		NVARCHAR(1000), 
 @CARD_SEQ		INT = 0

AS

SET NOCOUNT ON;

DECLARE @RTNRESULT SMALLINT, @RTNMSG VARCHAR(100)
DECLARE @UNAME VARCHAR(50), @UMAIL VARCHAR(100)

SELECT	TOP 1
		@UNAME = UNAME
	,	@UMAIL = UMAIL
--	,   @COMPANY_SEQ = COMPANY_SEQ
FROM	VIEW_USRINFO
WHERE	UID = @UID
AND		(COMPANY_SEQ IS NULL OR COMPANY_SEQ = @COMPANY_SEQ)
AND		(CASE WHEN @COMPANY_SEQ = 5001 THEN TBL_NAME ELSE '' END = CASE WHEN @COMPANY_SEQ = 5001 THEN 'S2_USERINFO' ELSE '' END)


IF EXISTS(SELECT SEQ FROM S4_EVENTMUSIC_STR WHERE SEQ = @EVENT_SEQ AND (CONVERT(VARCHAR(10), GETDATE(), 121) BETWEEN START_DATE AND END_DATE))
	BEGIN

		IF (SELECT DUPLICATION_YN FROM S4_EVENTMUSIC_STR WHERE SEQ = @EVENT_SEQ AND (COMPANY_SEQ IS NULL OR COMPANY_SEQ = @COMPANY_SEQ)) = 'Y'
			OR NOT EXISTS (SELECT * FROM S4_EVENTMUSIC_REPLY WHERE COMPANY_SEQ  = @COMPANY_SEQ AND REG_NUM = @EVENT_SEQ AND UID = @UID)
			BEGIN
				INSERT INTO S4_EVENTMUSIC_REPLY
				(
					COMPANY_SEQ, REG_NUM, UID, UNAME, UMAIL, COMMENT, CARD_SEQ
				)
				VALUES
				(
					@COMPANY_SEQ, @EVENT_SEQ, @UID, @UNAME, @UMAIL, @COMMENT, @CARD_SEQ
				)

				SET @RTNRESULT = 1
				SET @RTNMSG = '이벤트 참여에 성공하였습니다.'

			END
		ELSE
			BEGIN

				SET @RTNRESULT = 2
				SET @RTNMSG = '이미 이벤트에 참여 하셨습니다.'

			END
	END
ELSE
	BEGIN

		SET @RTNRESULT = 4
		SET @RTNMSG = '이벤트 기간이 아닙니다.'

	END



SELECT @RTNRESULT AS RESULT, @RTNMSG AS MSG
GO
