IF OBJECT_ID (N'dbo.SP_THECARD_EVENT_LUCKYBOX', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_THECARD_EVENT_LUCKYBOX
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		황새롬
-- Create date: 2017-11-28
-- Description:	더카드 산타를 잡아라 이벤트
-- EXEC [SP_THECARD_EVENT_LUCKYBOX] 's4guest'
-- =============================================

CREATE PROCEDURE [dbo].[SP_THECARD_EVENT_LUCKYBOX]
	@UID								AS VARCHAR(50)
AS
BEGIN

	-- 쿠폰세팅
	DECLARE		@COUPON_50000					AS	VARCHAR(50) = 'FAB1-2147-4B62-92D8'
		,		@COUPON_20000					AS	VARCHAR(50)	= '7F1D-6A78-4037-87EB'
		,		@COUPON_10000					AS	VARCHAR(50) = '46BB-161F-4BB9-AFA1'
		,		@COUPON_5000					AS	VARCHAR(50) = 'E928-A90A-493E-A6E4'
		,		@COUPON_STARBUCKS				AS	VARCHAR(50) = '4960-2F2A-4CAF-978A'
		,		@MSG							AS	VARCHAR(500) = ''
		,		@SELECT_COUPON_CODE				AS	VARCHAR(50) = 'FAIL'
		,		@SELECT_COUPON_STR				AS	VARCHAR(50) = ''
		,		@COUPON_50000_CNT				AS	INT			= 0
		,		@COUPON_20000_CNT				AS	INT			= 0
		,		@COUPON_10000_CNT				AS	INT			= 0
		,		@COUPON_5000_CNT				AS	INT			= 0
		,		@COUPON_STARBUCKS_CNT			AS	INT			= 0	
		,		@TOTAL_COUNT					AS	INT			= 0			
		,		@BASE_COUNT						AS	INT			= 30
		,		@COUPON_50000_BASE_COUNT		AS	INT			= 2
		,		@COUPON_20000_BASE_COUNT		AS	INT			= 5
		,		@COUPON_STARBUCKS_BASE_COUNT	AS	INT			= 3
		,		@COUPON_10000_BASE_COUNT		AS	INT			= 8
		,		@COUPON_5000_BASE_COUNT			AS	INT			= 12
		,		@RANDOM_VALUE					AS	INT			= 0
		,		@RANDOM_COUNT					AS	INT			= 0
		,		@COUPON_50000_CALC_COUNT		AS	INT			= 0
		,		@COUPON_20000_CALC_COUNT		AS	INT			= 0
		,		@COUPON_STARBUCKS_CALC_COUNT	AS	INT			= 0
		,		@COUPON_10000_CALC_COUNT		AS	INT			= 0
		,		@COUPON_5000_CALC_COUNT			AS	INT			= 0	
		,		@RESULT_CODE					AS	VARCHAR(4)	= '0000'

	-- 오늘 발급된 쿠폰이 있는 지 확인
	IF	NOT EXISTS(
		SELECT		*
		FROM		VW_COUPON_USER_LIST
		WHERE		1 = 1
		AND			UID = @UID
		AND			CONVERT(VARCHAR(10), REG_DATE, 120) = CONVERT(VARCHAR(10), GETDATE(), 120)
		AND			COUPON_CODE IN (	@COUPON_50000
									,	@COUPON_20000
									,	@COUPON_10000
									,	@COUPON_5000
									,	@COUPON_STARBUCKS	)
	)

	-- 발급이력이 없다면
	BEGIN

		-- 발급된 쿠폰 수 출력
		SELECT		@COUPON_50000_CNT		= ISNULL(SUM(CASE COUPON_CODE WHEN @COUPON_50000 THEN 1 ELSE 0 END), 0) 
			,		@COUPON_20000_CNT		= ISNULL(SUM(CASE COUPON_CODE WHEN @COUPON_20000 THEN 1 ELSE 0 END), 0) 
			,		@COUPON_10000_CNT		= ISNULL(SUM(CASE COUPON_CODE WHEN @COUPON_10000 THEN 1 ELSE 0 END), 0) 
			,		@COUPON_5000_CNT		= ISNULL(SUM(CASE COUPON_CODE WHEN @COUPON_5000  THEN 1 ELSE 0 END), 0) 
			,		@COUPON_STARBUCKS_CNT	= ISNULL(SUM(CASE COUPON_CODE WHEN @COUPON_STARBUCKS THEN 1 ELSE 0 END), 0) 
			,		@TOTAL_COUNT			= COUNT(*) 
		FROM		VW_COUPON_USER_LIST
		WHERE		COUPON_CODE IN (	@COUPON_50000
									,	@COUPON_20000
									,	@COUPON_10000
									,	@COUPON_5000
									,	@COUPON_STARBUCKS	)

		-- 계산할 값들 세팅
		SET		@RANDOM_COUNT				=	((@TOTAL_COUNT / @BASE_COUNT) + 1) * @BASE_COUNT;
		SET		@RANDOM_VALUE				=	CONVERT(INT,RAND() * @RANDOM_COUNT);	
		SET		@COUPON_50000_CALC_COUNT	=	((@TOTAL_COUNT / @BASE_COUNT) + 1) * @COUPON_50000_BASE_COUNT;
		SET		@COUPON_STARBUCKS_CALC_COUNT=	(((@TOTAL_COUNT / @BASE_COUNT) + 1) * @COUPON_STARBUCKS_BASE_COUNT) + @COUPON_50000_CALC_COUNT;
		SET		@COUPON_20000_CALC_COUNT	=	(((@TOTAL_COUNT / @BASE_COUNT) + 1) * @COUPON_20000_BASE_COUNT) + @COUPON_STARBUCKS_CALC_COUNT;
		SET		@COUPON_10000_CALC_COUNT	=	(((@TOTAL_COUNT / @BASE_COUNT) + 1) * @COUPON_10000_BASE_COUNT) + @COUPON_20000_CALC_COUNT;
		SET		@COUPON_5000_CALC_COUNT		=	(((@TOTAL_COUNT / @BASE_COUNT) + 1) * @COUPON_5000_BASE_COUNT) + @COUPON_10000_CALC_COUNT;

		-- 5만원
		IF		@RANDOM_VALUE > 0	
		AND		@RANDOM_VALUE < @COUPON_50000_CALC_COUNT + 1 
		AND		@COUPON_50000_CNT < @COUPON_50000_CALC_COUNT
		BEGIN
			SET	@SELECT_COUPON_CODE = @COUPON_50000
			SET	@SELECT_COUPON_STR = '50000'
		END

		-- 스벅컵
		ELSE IF @RANDOM_VALUE > @COUPON_50000_CALC_COUNT
		AND		@RANDOM_VALUE < @COUPON_STARBUCKS_CALC_COUNT + 1
		AND		@COUPON_STARBUCKS_CNT < @COUPON_STARBUCKS_CALC_COUNT - @COUPON_50000_CALC_COUNT
		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_STARBUCKS
			SET	@SELECT_COUPON_STR = 'STARBUCKS'
		END

		-- 2만원
		ELSE IF @RANDOM_VALUE > @COUPON_STARBUCKS_CALC_COUNT 
		AND		@RANDOM_VALUE < @COUPON_20000_CALC_COUNT + 1 
		AND		@COUPON_20000_CNT < @COUPON_20000_CALC_COUNT - @COUPON_STARBUCKS_CALC_COUNT
		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_20000
			SET	@SELECT_COUPON_STR = '20000'
		END

		-- 만원
		ELSE IF @RANDOM_VALUE > @COUPON_20000_CALC_COUNT  
		AND		@RANDOM_VALUE < @COUPON_10000_CALC_COUNT + 1 
		AND		@COUPON_10000_CNT < @COUPON_10000_CALC_COUNT - @COUPON_20000_CALC_COUNT
		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_10000
			SET	@SELECT_COUPON_STR = '10000'
		END

		-- 5천원
		ELSE IF @RANDOM_VALUE > @COUPON_10000_CALC_COUNT 
		AND @RANDOM_VALUE < @COUPON_5000_CALC_COUNT + 1 
		AND @COUPON_5000_CNT < @COUPON_5000_CALC_COUNT - @COUPON_10000_CALC_COUNT
		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_5000
			SET	@SELECT_COUPON_STR = '5000'
		END

		ELSE
		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_5000
			SET	@SELECT_COUPON_STR = '5000'
		END

		-- 기프티콘 받은 내역이 있을 경우 강제로 5000원쿠폰 반환
		IF	EXISTS(
			SELECT		*
			FROM		VW_COUPON_USER_LIST
			WHERE		1 = 1
			AND			UID = @UID
			AND			COUPON_CODE =	@COUPON_STARBUCKS
		)

		BEGIN
			SET @SELECT_COUPON_CODE = @COUPON_5000
			SET	@SELECT_COUPON_STR = '5000'
		END

        SET NOCOUNT ON 
		-- 프로시저 값 임시테이블 저장법
		DECLARE	@TABLETEMP TABLE
								(	TMP_RESULT_CODE VARCHAR(4)
								,	TMP_MSG VARCHAR(500))
		-- 쿠폰발급
		INSERT @TABLETEMP EXEC SP_EXEC_COUPON_ISSUE_FOR_ONE 5007, 'ST', @UID, @SELECT_COUPON_CODE
		
		--SET		@RESULT_CODE = (SELECT TMP_RESULT_CODE FROM @TABLETEMP);
		--SET		@MSG		 = (SELECT TMP_MSG FROM @TABLETEMP);
        
        -- 20171213 로그기록세팅
        DECLARE @COUPON_LOG_CODE AS VARCHAR(100);
        DECLARE @COUPON_LOG_MSG AS VARCHAR(100)
		SET		@COUPON_LOG_CODE = (SELECT TMP_RESULT_CODE FROM @TABLETEMP);
		SET		@COUPON_LOG_MSG		 = (SELECT TMP_MSG FROM @TABLETEMP);
        INSERT INTO LOG_MST (GUID, SITE, LOCATION, SUB_LOCATION, LOG_TYPE_NAME, MSG, USER_ID)  VALUES (
        'LUCKYBOX', '더카드', 'SELECT_COUPON_CODE=' + @SELECT_COUPON_CODE, 
        'SELECT_COUPON_STR=' + @SELECT_COUPON_STR, '@COUPON_LOG_CODE=' + @COUPON_LOG_CODE, 'COUPON_LOG_MSG=' + @COUPON_LOG_MSG, @UID); 
        ------------------------

        SET NOCOUNT OFF

		SET		@RESULT_CODE = '0000';
		SET		@MSG		= '쿠폰이 발송되었습니다.';
        
	END

	ELSE

	BEGIN
		SET		@RESULT_CODE = '6666';
		SET		@MSG		= '응모횟수는 1일 1회입니다.';
	END

	SELECT	@MSG	AS	MSG
		,	@RESULT_CODE AS RESULT_CODE
		,	@SELECT_COUPON_CODE AS COUPON_CODE
		,	@SELECT_COUPON_STR AS COUPON_STR

END

GO
