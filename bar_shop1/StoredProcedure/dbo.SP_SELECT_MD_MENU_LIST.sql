IF OBJECT_ID (N'dbo.SP_SELECT_MD_MENU_LIST', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_SELECT_MD_MENU_LIST
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*

EXEC SP_SELECT_MD_MENU_LIST

*/

CREATE PROCEDURE [dbo].[SP_SELECT_MD_MENU_LIST]

AS
BEGIN

	SET NOCOUNT ON;

	WITH MD_MENU_LIST AS 
	( 
		SELECT	*
		FROM	(
					SELECT	DISTINCT
							COMPANY_SEQ AS ID
						,	0 AS PARENT_ID
						,	COMPANY_SEQ
						,	CONVERT(VARCHAR(1000),
								CASE 
									WHEN COMPANY_SEQ = 5000 THEN '바른손몰'
									WHEN COMPANY_SEQ = 5001 THEN '바른손카드'
									WHEN COMPANY_SEQ = 5003 THEN '프리미어페이퍼'
									-- WHEN COMPANY_SEQ = 5006 THEN '비핸즈카드'
									WHEN COMPANY_SEQ = 5007 THEN '더카드'
									WHEN COMPANY_SEQ = 7717 THEN '디얼디어'
									ELSE ''
								END
							) AS TITLE
						,	CONVERT(VARCHAR(1000), COMPANY_SEQ) AS SORT
						,	CONVERT(VARCHAR(1000), '1') AS DEPTH
						,	CONVERT(VARCHAR(1000), '') AS LINK_URL
					FROM	S4_MD_CHOICE_STR 
					WHERE	1 = 1
					AND		COMPANY_SEQ NOT IN (1437, 5006)

				) A

		UNION ALL 
	
		SELECT	SUB_MML.ID
			,	SUB_MML.PARENT_ID
			,	SUB_MML.COMPANY_SEQ
			,	SUB_MML.TITLE
			,	CONVERT(VARCHAR(1000), CONVERT(NVARCHAR, MML.SORT) + ' > ' + RIGHT('0000' + CONVERT(VARCHAR(255), SUB_MML.ID), 4)) SORT
			,	CONVERT(VARCHAR(1000), MML.DEPTH + '_' + SUB_MML.DEPTH) AS DEPTH
			,	SUB_MML.LINK_URL 
		FROM	(
					SELECT	DISTINCT
							COMPANY_SEQ AS ID
						,	0 AS PARENT_ID
						,	COMPANY_SEQ
						,	CONVERT(VARCHAR(1000),
								CASE 
									WHEN COMPANY_SEQ = 5000 THEN '바른손몰'
									WHEN COMPANY_SEQ = 5001 THEN '바른손카드'
									WHEN COMPANY_SEQ = 5003 THEN '프리미어페이퍼'
									WHEN COMPANY_SEQ = 5006 THEN '비핸즈카드'
									WHEN COMPANY_SEQ = 5007 THEN '더카드'
									WHEN COMPANY_SEQ = 7717 THEN '디얼디어'
									ELSE ''
								END
							) AS TITLE
						,	CONVERT(VARCHAR(1000), COMPANY_SEQ) AS SORT
						,	CONVERT(VARCHAR(1000), '1') AS DEPTH
						,	CONVERT(VARCHAR(1000), '') AS LINK_URL
					FROM	S4_MD_CHOICE_STR 
					WHERE	1 = 1
					AND		COMPANY_SEQ <> 1437
					UNION ALL
					SELECT	SMCS.MD_SEQ AS ID
						,	CASE WHEN SMCS.MD_UPPER_CODE = 0 THEN SMCS.COMPANY_SEQ ELSE SMCS.MD_UPPER_CODE END AS PARENT_ID
						,	SMCS.COMPANY_SEQ
						,	CONVERT(VARCHAR(1000), SMCS.MD_TEXT) AS TITLE
						,	RIGHT('0000' + CONVERT(VARCHAR(255), SMCS.COMPANY_SEQ), 4) AS SORT
						,	RIGHT('0000' + CAST(CASE WHEN SMCS.MD_UPPER_CODE = 0 THEN SMCS.COMPANY_SEQ ELSE SMCS.MD_UPPER_CODE END AS VARCHAR(1000)), 4) AS DEPTH
						,	CONVERT
							(
									VARCHAR(1000)
								,	CASE 
											WHEN SMCS.MD_UPPER_CODE = 0 THEN '' 
											ELSE 
													CASE 
															WHEN ISNULL(REPLACE(SMCS.LINK_URL, ' ', '') ,'') = ''
															THEN '' 
															ELSE SMCS.LINK_URL + '?MdSeq=' + REPLACE(STR(SMCS.MD_SEQ),' ','') + '&CompanySeq=' + REPLACE(STR(SMCS.COMPANY_SEQ),' ','')
													END
									END
							) AS LINK_URL 
					FROM	S4_MD_CHOICE_STR SMCS
					JOIN	S4_MD_CHOICE_STR_USEDYN SMCSU ON SMCS.MD_SEQ = SMCSU.MD_SEQ 
					WHERE	1 = 1
					AND		SMCSU.USED_YN='Y'
				) AS SUB_MML
			,	MD_MENU_LIST AS MML
		WHERE	SUB_MML.PARENT_ID = MML.ID
	)

	SELECT	CAST(ID AS INT) AS Id
		,	CAST(PARENT_ID AS INT) AS ParentId
		,	CAST(COMPANY_SEQ AS INT) AS CompanySeq
		,	TITLE AS Title
		,	SORT AS Sort
		,	DEPTH AS Depth
		,	LINK_URL AS LinkUrl
	FROM	MD_MENU_LIST ORDER BY SORT ASC


END
GO
