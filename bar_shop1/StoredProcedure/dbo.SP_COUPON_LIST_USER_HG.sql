IF OBJECT_ID (N'dbo.SP_COUPON_LIST_USER_HG', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_COUPON_LIST_USER_HG
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*
	Author		:	황새롬
	Create date	:	2017-07-18
	Description	:	마이페이지 통합쿠폰 리스트

	EXEC SP_COUPON_LIST_USER_HG 5001, 'jihye0316'

	기본쿠폰, 중복쿠폰, AD쿠폰 : 청첩장, 감사장, 초대장 카드할인금액에서 할인 (복수)
	추가쿠폰 : 기타 서비스 할인 (단수)

*/
CREATE PROCEDURE [dbo].[SP_COUPON_LIST_USER_HG]
		@COMPANY_SEQ				AS VARCHAR(4)
	,	@UID						AS VARCHAR(100)
AS
BEGIN
	
	SET NOCOUNT ON;

	SELECT	 CASE WHEN LTRIM(T.CMMN_CODE) ='식전영상' THEN LTRIM(T.CMMN_CODE) ELSE T.DISCOUNT END AS DISCOUNT
		   , CASE WHEN LTRIM(T.CMMN_CODE) ='식전영상' THEN T.COUPON_CODE ELSE T.COUPON_NAME END AS COUPON_NAME
		   , T.DTL_NAME
		   , T.START_DATE
		   , T.EXPIRY_TYPE
		   , T.EXPIRY_CUSTOM_VALUE
		   , T.END_DATE
		   , T.USE_DEVICE
		   , T.EXPIRES_YN
		   , T.COUPON_DESC
		   , T.CMMN_CODE
	FROM
	(
		SELECT	CC.DTL_NAME																	-- 쿠폰종류 (기본/중복/추가할인)
			,	REPLACE(CONVERT(VARCHAR,CONVERT(MONEY,DISCOUNT_VALUE),1),'.00','')
			+	CASE WHEN DISCOUNT_FIXED_RATE_TYPE = 'W' THEN '원' ELSE '%' END AS DISCOUNT	-- 할인액
			,	COUPON_NAME																	-- 쿠폰명
			,	CD.COUPON_CODE
			,	CONVERT(VARCHAR, EXPIRY_START_DATE, 23) AS START_DATE						-- 시작일
			,	EXPIRY_TYPE
			,	EXPIRY_CUSTOM_VALUE
			,	CASE EXPIRY_TYPE								
								WHEN 'V' THEN CONVERT(VARCHAR, CI.END_DATE, 23)
								WHEN 'P' THEN CONVERT(VARCHAR, EXPIRY_END_DATE, 23)
				END AS END_DATE																-- 종료일
			,	CASE USE_DEVICE WHEN 'A' THEN 'PC/MOBILE'
								WHEN 'P' THEN 'PC'
								WHEN 'M' THEN 'MOBILE'
				END AS USE_DEVICE															-- 사용처
			,	CASE WHEN END_DATE < GETDATE() THEN 'Y' ELSE 'N' END AS EXPIRES_YN			-- 기간만료 : Y , 기간남음 : N
			,	ACTIVE_YN																	-- 사용가능 : Y	, 사용완료 : N
			,	COUPON_DESC																	-- 쿠폰내용
			,	STUFF
				(
					(
						SELECT	', ' + CC_SERVICE.DTL_NAME 
						FROM	COUPON_APPLY_SERVICE CAS 
						JOIN	COMMON_CODE CC_SERVICE ON CAS.CMMN_CODE = CC_SERVICE.CMMN_CODE  
						WHERE	COUPON_MST_SEQ = CD.COUPON_MST_SEQ 
						AND		CC_SERVICE.CLSS_CODE != 132
						FOR XML PATH ('')
					),1,1,''
				) AS CMMN_CODE
			,	CI.REG_DATE
		FROM	COUPON_ISSUE	CI 
		JOIN	COUPON_DETAIL	CD	ON CI.COUPON_DETAIL_SEQ = CD.COUPON_DETAIL_SEQ
		JOIN	COUPON_MST		CM	ON CD.COUPON_MST_SEQ = CM.COUPON_MST_SEQ
		JOIN	COMMON_CODE		CC	ON CC.CMMN_CODE = CM.COUPON_TYPE_CODE
		WHERE	1 = 1
		AND		CI.UID = @UID
		AND		CI.COMPANY_SEQ = @COMPANY_SEQ
		AND		CI.ACTIVE_YN = 'Y'

	) T
	ORDER BY	T.EXPIRES_YN ASC
	,		T.REG_DATE DESC

END
GO
