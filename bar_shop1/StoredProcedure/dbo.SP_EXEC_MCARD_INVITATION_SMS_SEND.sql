IF OBJECT_ID (N'dbo.SP_EXEC_MCARD_INVITATION_SMS_SEND', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_EXEC_MCARD_INVITATION_SMS_SEND
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*

SELECT * FROM MCARD_INVITATION WHERE AUTHCODE = 'ukliio1'

UPDATE MCARD_INVITATION SET DELETEYN = 'N', SMSMYPAGEYN = 'N' WHERE AUTHCODE = 'ukliio1' AND INVITATIONID = 10489

EXEC SP_EXEC_MCARD_INVITATION_SMS_SEND 10489, 'SS2349145', 'ukliio1', 'http://asdasdasd.copm', '010-4934-9760'

*/
CREATE PROCEDURE [dbo].[SP_EXEC_MCARD_INVITATION_SMS_SEND]
    @P_INVITATION_ID AS VARCHAR(50)
,   @P_INVITATION_CODE AS VARCHAR(50)
,   @P_USER_ID AS VARCHAR(50)
,   @P_URL AS VARCHAR(100)
,   @P_HPHONE AS VARCHAR(15)

AS
BEGIN
    
    SET NOCOUNT ON;




    DECLARE @RESULT_SUCCESS AS VARCHAR(100)
	DECLARE @RESULT_MESSAGE AS VARCHAR(100)

	DECLARE @SMS_MESSAGE AS VARCHAR(100)
	DECLARE @INVITATION_TYPE AS VARCHAR(100)
	DECLARE @PHOTO_URL AS VARCHAR(100)

    IF EXISTS( SELECT TOP 1 * FROM MCARD_INVITATION WHERE INVITATIONCODE = @P_INVITATION_CODE AND INVITATIONID = @P_INVITATION_ID AND AUTHCODE = @P_USER_ID AND SMSMYPAGEYN = 'N' AND DELETEYN = 'N')
    BEGIN
        
		SELECT	@INVITATION_TYPE = INVITATIONTYPE
			,	@PHOTO_URL = ISNULL('\Inetpub\MobileInvitation\Photos\' + (SUBSTRING(CONVERT(VARCHAR(10), COMPLETEDTIME  ,  23) ,1,4) + SUBSTRING(CONVERT(VARCHAR(10), COMPLETEDTIME  ,  23) ,6,2) + '/' + INVITATIONCODE + '/mmscard.jpg'), '')
		FROM	MCARD_INVITATION 
		WHERE	1 = 1
		AND		INVITATIONCODE = @P_INVITATION_CODE 
		AND		INVITATIONID = @P_INVITATION_ID 
		AND		AUTHCODE = @P_USER_ID 
		AND		SMSMYPAGEYN = 'N'

		IF @INVITATION_TYPE = 'IWED'
		BEGIN

			SET @SMS_MESSAGE = @P_URL + ' ' + ISNULL((SELECT GroomName + '♡' + BrideName + '의 모바일청첩장입니다.' FROM mcard_InvitationWedding WHERE INVITATIONID = @P_INVITATION_ID), '')

		END

		ELSE
		BEGIN

			SET @SMS_MESSAGE = @P_URL + ' ' + ISNULL(( SELECT GroupName + ' 모바일초대장 입니다.' FROM mcard_InvitationParty WHERE INVITATIONID = @P_INVITATION_ID ), '')

		END

		EXEC sp_DacomMMS @P_HPHONE, '16440708', @SMS_MESSAGE , @PHOTO_URL

		UPDATE	MCARD_INVITATION
		SET		SMSMYPAGEYN = 'Y'
		WHERE	1 = 1
		AND		INVITATIONID = @P_INVITATION_ID

		SET @RESULT_SUCCESS = 'success'
		SET @RESULT_MESSAGE = '전송 완료'
    END

    ELSE
    BEGIN
        
		IF EXISTS( SELECT TOP 1 * FROM MCARD_INVITATION WHERE INVITATIONCODE = @P_INVITATION_CODE AND INVITATIONID = @P_INVITATION_ID AND AUTHCODE = @P_USER_ID AND SMSMYPAGEYN = 'Y' AND DELETEYN = 'N')
		BEGIN
			
			SET @RESULT_SUCCESS = 'sended'
			SET @RESULT_MESSAGE = '이미 전송 함'

		END

		ELSE
		BEGIN
			
			SET @RESULT_SUCCESS = 'failed'
			SET @RESULT_MESSAGE = '전송 실패'

		END

    END



    SELECT  @RESULT_SUCCESS AS RESULT_SUCCESS
		,	@RESULT_MESSAGE AS RESULT_MESSAGE



END
GO
