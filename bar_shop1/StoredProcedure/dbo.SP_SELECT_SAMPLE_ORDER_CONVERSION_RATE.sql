IF OBJECT_ID (N'dbo.SP_SELECT_SAMPLE_ORDER_CONVERSION_RATE', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_SELECT_SAMPLE_ORDER_CONVERSION_RATE
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*

EXEC SP_SELECT_SAMPLE_ORDER_CONVERSION_RATE '2016-01', '2017-10', ''

*/
CREATE PROCEDURE [dbo].[SP_SELECT_SAMPLE_ORDER_CONVERSION_RATE]
		@P_START_DATE AS VARCHAR(7)
	,	@P_END_DATE AS VARCHAR(7)
	,	@P_SALES_GUBUN AS VARCHAR(100)
AS
BEGIN

	SET NOCOUNT ON

	DECLARE @T_MONTH TABLE
    (
		[MONTH] VARCHAR(7)
	)	

	INSERT INTO @T_MONTH ([MONTH])

	SELECT	CONVERT(VARCHAR(7), DATEADD(D, NUMBER, @P_START_DATE + '-01'), 120) [MONTH]
	FROM	MASTER..SPT_VALUES
	WHERE	TYPE = 'P' 
	AND		NUMBER <= DATEDIFF(D, @P_START_DATE + '-01', @P_END_DATE + '-01')

	GROUP BY CONVERT(VARCHAR(7), DATEADD(D,NUMBER,@P_START_DATE + '-01'), 120)

	

	SET @P_SALES_GUBUN = CASE WHEN @P_SALES_GUBUN = 'SB|SA|ST|SS|B' THEN '' ELSE @P_SALES_GUBUN END



	SELECT	'SAMPLE_DELIVERY_DATE' AS ListType
		,	TM.MONTH AS SampleDeliveryDate
		,	CASE WHEN ISNULL(SUM(A.WEDDING_INVITATION_QNT), 0) = 0 THEN 0 ELSE COUNT(*) END AS SampleCount
		,	ISNULL(SUM(A.WEDDING_INVITATION_QNT), 0) AS WeddingInvitationCount
		,	ISNULL((1.0 * SUM(A.WEDDING_INVITATION_QNT) / COUNT(*)) * 100, 0) AS Rate

	FROM	@T_MONTH TM
	LEFT 
	JOIN	(
				SELECT	CSO.SAMPLE_ORDER_SEQ
					,	MAX(CASE WHEN CO.MEMBER_ID IS NULL THEN 0 ELSE 1 END) AS WEDDING_INVITATION_QNT
					,	MAX(CSO.DELIVERY_DATE) AS SAMPLE_DELIVERY_DATE

				FROM	CUSTOM_SAMPLE_ORDER CSO
				JOIN	VW_USER_INFO VUI
					ON	CSO.MEMBER_ID = VUI.UID
				LEFT
				JOIN	CUSTOM_ORDER CO 
					ON		CSO.MEMBER_ID = CO.MEMBER_ID 
					AND		CSO.DELIVERY_DATE IS NOT NULL
					AND		CO.SRC_SEND_DATE IS NOT NULL
					AND		CSO.DELIVERY_DATE <= CO.SRC_SEND_DATE
					AND		CSO.SALES_GUBUN = CO.SALES_GUBUN
					AND		CO.STATUS_SEQ = 15
					AND		CO.ORDER_TYPE IN (1,3,6,7)

				WHERE	1 = 1
				AND		CSO.STATUS_SEQ = 12

				AND		(@P_SALES_GUBUN = '' OR CSO.SALES_GUBUN IN (SELECT VALUE FROM DBO.FN_SPLIT(@P_SALES_GUBUN, '|')))
				AND		CSO.DELIVERY_DATE >= @P_START_DATE + '-01 00:00:00'
				AND		CSO.DELIVERY_DATE < DATEADD(MM, 1, @P_END_DATE + '-01 00:00:00')

				GROUP BY CSO.SAMPLE_ORDER_SEQ
			) A ON TM.MONTH = CONVERT(VARCHAR(7), A.SAMPLE_DELIVERY_DATE, 120)

	GROUP BY TM.MONTH
	
	UNION ALL

	SELECT	'WEDDING_DATE' AS ListType
		,	TM.MONTH AS SampleDeliveryDate
		,	CASE WHEN ISNULL(SUM(A.WEDDING_INVITATION_QNT), 0) = 0 THEN 0 ELSE COUNT(*) END AS SampleCount
		,	ISNULL(SUM(A.WEDDING_INVITATION_QNT), 0) AS WeddingInvitationCount
		,	ISNULL((1.0 * SUM(A.WEDDING_INVITATION_QNT) / COUNT(*)) * 100, 0) AS Rate

	FROM	@T_MONTH TM
	LEFT
	JOIN	(
				SELECT	CSO.SAMPLE_ORDER_SEQ
					,	MAX(CASE WHEN CO.MEMBER_ID IS NULL THEN 0 ELSE 1 END) AS WEDDING_INVITATION_QNT
					,	MAX(VUI.WEDD_YEAR + '-' + RIGHT('0' + VUI.WEDD_MONTH, 2) + '-' + RIGHT('0' + VUI.WEDD_DAY, 2)) AS SAMPLE_DELIVERY_DATE

				FROM	CUSTOM_SAMPLE_ORDER CSO
				JOIN	VW_USER_INFO VUI
					ON	CSO.MEMBER_ID = VUI.UID
				LEFT
				JOIN	CUSTOM_ORDER CO 
					ON		CSO.MEMBER_ID = CO.MEMBER_ID 
					AND		CSO.DELIVERY_DATE IS NOT NULL
					AND		CO.SRC_SEND_DATE IS NOT NULL
					AND		CSO.DELIVERY_DATE <= CO.SRC_SEND_DATE
					AND		CSO.SALES_GUBUN = CO.SALES_GUBUN
					AND		CO.STATUS_SEQ = 15
					AND		CO.ORDER_TYPE IN (1,3,6,7)

				WHERE	1 = 1
				AND		CSO.STATUS_SEQ = 12
				
				AND		(@P_SALES_GUBUN = '' OR CSO.SALES_GUBUN IN (SELECT VALUE FROM DBO.FN_SPLIT(@P_SALES_GUBUN, '|')))
				AND		ISDATE(VUI.WEDD_YEAR + '-' + RIGHT('0' + VUI.WEDD_MONTH, 2) + '-' + RIGHT('0' + VUI.WEDD_DAY, 2)) = 1
				AND		VUI.WEDD_YEAR + '-' + RIGHT('0' + VUI.WEDD_MONTH, 2) + '-' + RIGHT('0' + VUI.WEDD_DAY, 2) >= @P_START_DATE + '-01'
				AND		VUI.WEDD_YEAR + '-' + RIGHT('0' + VUI.WEDD_MONTH, 2) + '-' + RIGHT('0' + VUI.WEDD_DAY, 2) < CONVERT(VARCHAR(10), DATEADD(MM, 1, @P_END_DATE + '-01 00:00:00'), 120)

				GROUP BY CSO.SAMPLE_ORDER_SEQ
			) A ON TM.MONTH = CONVERT(VARCHAR(7), A.SAMPLE_DELIVERY_DATE, 120)
	
	GROUP BY TM.MONTH

	ORDER BY TM.MONTH ASC

END














GO
