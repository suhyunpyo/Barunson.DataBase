IF OBJECT_ID (N'dbo.SP_EXEC_S2_CARD_ERP_STOCK', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_EXEC_S2_CARD_ERP_STOCK
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*

EXEC SP_SELECT_S2_CARD_WITH_ERP_STOCK

EXEC SP_EXEC_S2_CARD_ERP_STOCK

SELECT	*
FROM	S2_CARD_ERP_STOCK

DELETE	
FROM	S2_CARD_ERP_STOCK

SELECT	*
FROM	S2_CARD SC
LEFT JOIN	S2_CARD_ERP_STOCK SCEC ON SC.CARD_CODE = SCEC.CARD_CODE AND SC.CARD_ERPCODE = SCEC.CARD_CODE_ERP



*/

CREATE PROCEDURE [dbo].[SP_EXEC_S2_CARD_ERP_STOCK]

AS

	SET NOCOUNT ON
	
	DECLARE @ERP_DATE_RANGE AS VARCHAR(8) = CONVERT(VARCHAR(8), GETDATE(), 112)
	
	CREATE TABLE #TEMP_ERP_STOCK
	(
			ITEMCODE VARCHAR(50)
		,	STATUSCODE VARCHAR(50)
		,	C_ITEMGUBUN VARCHAR(50)
		,	C_ITEMBRAND VARCHAR(50)
		,	C_ITEMORIGIN VARCHAR(50)
		,	C_ITEMSTATUS VARCHAR(100)
		,	OLDBRANDNAME VARCHAR(50)

		,	STDPRICE NUMERIC(28, 8)
		,	ERP_SOBI NUMERIC(28, 8)
		,	CARDSET_PRICE NUMERIC(28, 8)

		,	BILLQTY NUMERIC(28, 8)
		,	OHQTY NUMERIC(28, 8)
		,	REQUESTQTY NUMERIC(28, 8)
		,	[가용재고] NUMERIC(28, 8)
		,	[미생산량] NUMERIC(28, 8)
		,	[중국재고] NUMERIC(28, 8)
		,	[이동중재고] NUMERIC(28, 8)

		,	BARUNDIS VARCHAR(20)
		,	BHANDSDIS VARCHAR(20)
		,	THEDIS VARCHAR(20)
		,	PREDIS VARCHAR(20)
		,	BAMDIS VARCHAR(20)
		,	ONLINECODE VARCHAR(50)

		,	SALE30DAYQTY NUMERIC(28, 8)
		,	SALE90DAYQTY NUMERIC(28, 8)
		,	SALE180DAYQTY NUMERIC(28, 8)
		,	SALE365DAYQTY NUMERIC(28, 8)
		,	REGISTRYDATE VARCHAR(8)
		,	RELEASEDATE VARCHAR(8)
		,	Card_Image VARCHAR(100)
	)

	INSERT INTO #TEMP_ERP_STOCK
	EXEC	[XERP].DBO.[C_spSMART_INVENTORY] @ERP_DATE_RANGE, @ERP_DATE_RANGE, null, null,  null,  null, null, null, null, null, null, N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', N'Y', null, null



	-- ERP 신규 데이타 INSERT
	INSERT INTO S2_CARD_ERP_STOCK	(
			CARD_CODE
		,	CARD_CODE_ERP
		,	CARD_TYPE_NAME
--		,	BRAND_NAME
		,	BRAND_NAME_OLD
		,	ORIGIN_NAME
		,	PRODUCTION_STATUS_NAME

		,	CLOSING_COST
		,	CONSUMER_PRICE
		,	CARD_SET_PRICE

		,	INVENTORY_CURRENT_QTY
		,	INVENTORY_REQUEST_QTY
		,	INVENTORY_AVAILABLE_QTY
		,	INVENTORY_NOT_MAKE_QTY
		,	INVENTORY_CHINA_QTY
		,	INVENTORY_MOVING_QTY

		,	TOTAL_SALE_PRICE_30_DAY
		,	TOTAL_SALE_PRICE_90_DAY
		,	TOTAL_SALE_PRICE_180_DAY
		,	TOTAL_SALE_PRICE_365_DAY

		,	ERP_FIRST_REG_DATE
		,	RELEASE_DATE
		,	MOD_DATE
		,	REG_DATE
		
	)
	SELECT	DISTINCT
			ONLINECODE
		,	ITEMCODE
		,	C_ITEMGUBUN
--		,	C_ITEMBRAND
		,	OLDBRANDNAME
		,	C_ITEMORIGIN
		,	C_ITEMSTATUS
		
		,	CONVERT(NUMERIC(18, 2), STDPRICE)
		,	CONVERT(INT, ROUND(ERP_SOBI, 0))
		,	CONVERT(INT, ROUND(CARDSET_PRICE, 0))

		,	CONVERT(INT, ROUND(OHQTY, 0))
		,	CONVERT(INT, ROUND(REQUESTQTY, 0))
		,	CONVERT(INT, ROUND([가용재고], 0))
		,	CONVERT(INT, ROUND([미생산량], 0))
		,	CONVERT(INT, ROUND([중국재고], 0))
		,	CONVERT(INT, ROUND([이동중재고], 0))

		,	CONVERT(INT, ROUND(SALE30DAYQTY, 0)) 
		,	CONVERT(INT, ROUND(SALE90DAYQTY, 0)) 
		,	CONVERT(INT, ROUND(SALE180DAYQTY, 0))
		,	CONVERT(INT, ROUND(SALE365DAYQTY, 0))

		,	CONVERT(DATETIME, REGISTRYDATE)
		,	CONVERT(DATETIME, RELEASEDATE)
		,	GETDATE()
		,	GETDATE()

	FROM	#TEMP_ERP_STOCK TES
	WHERE	1 = 1
	AND		NOT EXISTS (	
							SELECT	* 
							FROM	S2_CARD_ERP_STOCK SCES 
							WHERE	1 = 1
							AND		TES.ITEMCODE = SCES.CARD_CODE_ERP 
							AND		TES.ONLINECODE = SCES.CARD_CODE 
						)
	AND		TES.OhQty IS NOT NULL 
	AND		TES.ONLINECODE IS NOT NULL
	AND		TES.ONLINECODE <> ''
--    AND     ONLINECODE NOT IN ('DDC_3259')  --20230117 임시 예외처리 변미정


	

	-- ERP DATA UPDATE
	UPDATE	S2_CARD_ERP_STOCK
	SET		
			CARD_TYPE_NAME				= TES.C_ITEMGUBUN
		,	BRAND_NAME					= TES.C_ITEMBRAND
		,	BRAND_NAME_OLD				= TES.OLDBRANDNAME
		,	ORIGIN_NAME					= TES.C_ITEMORIGIN
		,	PRODUCTION_STATUS_NAME		= TES.C_ITEMSTATUS

		,	CLOSING_COST				= CONVERT(NUMERIC(18, 2), STDPRICE)
		,	CONSUMER_PRICE				= CONVERT(INT, ROUND(ERP_SOBI, 0))
		,	CARD_SET_PRICE				= CONVERT(INT, ROUND(CARDSET_PRICE, 0))
		
		,	INVENTORY_CURRENT_QTY		= CONVERT(INT, ROUND(OHQTY, 0))
		,	INVENTORY_REQUEST_QTY		= CONVERT(INT, ROUND(REQUESTQTY, 0))
		,	INVENTORY_AVAILABLE_QTY		= CONVERT(INT, ROUND([가용재고], 0))
		,	INVENTORY_NOT_MAKE_QTY		= CONVERT(INT, ROUND([미생산량], 0))
		,	INVENTORY_CHINA_QTY			= CONVERT(INT, ROUND([중국재고], 0))
		,	INVENTORY_MOVING_QTY		= CONVERT(INT, ROUND([이동중재고], 0))

		,	TOTAL_SALE_PRICE_30_DAY		= CONVERT(INT, ROUND(SALE30DAYQTY, 0))  
		,	TOTAL_SALE_PRICE_90_DAY		= CONVERT(INT, ROUND(SALE90DAYQTY, 0)) 
		,	TOTAL_SALE_PRICE_180_DAY	= CONVERT(INT, ROUND(SALE180DAYQTY, 0))
		,	TOTAL_SALE_PRICE_365_DAY	= CONVERT(INT, ROUND(SALE365DAYQTY, 0))
		,	RELEASE_DATE				= CONVERT(DATETIME, RELEASEDATE)
		,	MOD_DATE					= GETDATE()
	FROM	S2_CARD_ERP_STOCK SCES
	JOIN	#TEMP_ERP_STOCK TES ON SCES.CARD_CODE = TES.ONLINECODE AND SCES.CARD_CODE_ERP = TES.ITEMCODE



	DROP TABLE #TEMP_ERP_STOCK

	EXEC SP_EXEC_S2_CARD_ERP_STOCK_DEARDEER
GO
