IF OBJECT_ID (N'dbo.SP_SELECT_MD_ITEM_LIST', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_SELECT_MD_ITEM_LIST
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*

EXEC SP_SELECT_MD_ITEM_LIST 465, 5000, 'S'

EXEC SP_SELECT_MD_ITEM_LIST 36, 5006, 'M'

*/

CREATE PROCEDURE [dbo].[SP_SELECT_MD_ITEM_LIST]
		@P_MD_SEQ			AS INT
	,	@P_COMPANY_SEQ		AS INT
	,	@P_BRAND_KIND		AS VARCHAR(10) = ''
AS
BEGIN

	DECLARE @GRADE			AS VARCHAR(10) = ''
	DECLARE @GUBUN			AS VARCHAR(10) = ''
	DECLARE @CARD_BRAND		AS VARCHAR(10) = ''

	/* - Start - BarunsonMall 메인 상품관리를 위한 설정 */
	SET @GRADE = CASE 
						WHEN @P_MD_SEQ IN ( 426, 427, 428, 429 ) THEN 'A'
						WHEN @P_MD_SEQ IN ( 431, 432, 433, 434 ) THEN 'B'
						WHEN @P_MD_SEQ IN ( 436 ) THEN 'C'
						ELSE ''
				 END

	SET @GUBUN = CASE 
						WHEN @P_MD_SEQ IN ( 426, 431, 436 ) THEN 'BC'
						WHEN @P_MD_SEQ IN ( 427, 432 ) THEN 'B'
						WHEN @P_MD_SEQ IN ( 428, 433 ) THEN 'N'
						WHEN @P_MD_SEQ IN ( 429, 434 ) THEN 'S'
						ELSE ''
				 END

	-- 426, 431, 436 (Brand Collection) 인데, @P_BRAND_KIND 값이 공백이면 'B'로 설정
	SET @P_BRAND_KIND =	CASE WHEN @P_MD_SEQ IN ( 426, 431, 436 ) AND @P_BRAND_KIND = '' THEN 'B' ELSE @P_BRAND_KIND END
	/* - End - */

	-- S2Card.CardBrand 를 검색해야 하는 경우
	SET @CARD_BRAND		= CASE WHEN @GRADE = '' AND @GUBUN = '' AND @P_BRAND_KIND <> ''  THEN @P_BRAND_KIND ELSE @CARD_BRAND END
	SET @P_BRAND_KIND	= CASE WHEN @GRADE = '' AND @GUBUN = '' AND @P_BRAND_KIND <> ''  THEN ''			ELSE @P_BRAND_KIND END

	SET NOCOUNT ON

	SELECT  SMC.MD_SEQ													AS MdSeq
		,	SMC.SEQ														AS Seq
		,	MAX(SMC.CARD_SEQ)											AS CardSeq
		,	MAX(SMC.LINK_URL)											AS LinkUrl
		,	MAX(SMC.LINK_TARGET)										AS LinkTarget
		,	MAX(SMC.CARD_TEXT)											AS CardText
		,	MAX(ISNULL(SMC.CUSTOM_IMG, ''))								AS CustomImg
		,	MAX(SMC.SORTING_NUM)										AS SortingNum
		,	MAX(SMC.CLICK_COUNT)										AS ClickCount
		,	MAX(SC.CARD_CODE)											AS CardCode
		,	MAX(SC.CARD_NAME)											AS CardName
		,	MAX(SC.CARDSET_PRICE)										AS CardSetPrice
		,	MAX(SC.CARDBRAND)											AS CardBrand
		,	MAX(SCSS.COMPANY_SEQ)										AS CompanySeq
		,	CASE WHEN MAX(SCSS.ISDISPLAY)	= 1 THEN 'Y' ELSE 'N' END	AS IsDisplay
		,	CASE WHEN MAX(SCSS.ISJUMUN)		= 1 THEN 'Y' ELSE 'N' END	AS IsJumun
		,	CASE WHEN MAX(SCSS.ISNEW)		= 1 THEN 'Y' ELSE 'N' END	AS IsNew
		,	CASE WHEN MAX(SCSS.ISBEST)		= 1 THEN 'Y' ELSE 'N' END	AS IsBest
		,	CASE WHEN MAX(SCSS.ISEXTRA)		= 1 THEN 'Y' ELSE 'N' END	AS IsExtra

		,	CASE	
					WHEN MAX(SCSS.COMPANY_SEQ) = 5000 THEN 'http://file.barunsoncard.com/barunsonmall/'	+ MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
					WHEN MAX(SCSS.COMPANY_SEQ) = 5001 THEN 'http://file.barunsoncard.com/barunsoncard/' + MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
					WHEN MAX(SCSS.COMPANY_SEQ) = 5003 THEN 'http://file.barunsoncard.com/story/'		+ MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
					WHEN MAX(SCSS.COMPANY_SEQ) = 5006 THEN 'http://file.barunsoncard.com/bhandscard/'   + MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
					WHEN MAX(SCSS.COMPANY_SEQ) = 5007 THEN 'http://file.barunsoncard.com/thecard/'		+ MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
					ELSE								   'http://file.barunsoncard.com/barunsoncard/' + MAX(SC.CARD_CODE) + '/' + MAX(SCI.CARDIMAGE_FILENAME)
			END
			AS CardImageFileName

		,	MAX(ISNULL(SCD.DISCOUNT_RATE, -1))										AS DiscountRate
		,	(SELECT COUNT(*) FROM CUSTOM_ORDER WHERE STATUS_SEQ = 15 AND (ORDER_DATE > DATEADD(M ,-3, GETDATE())) AND CARD_SEQ = MAX(SMC.CARD_SEQ)) AS SalesCount
		,	(SELECT COUNT(*) FROM CUSTOM_ORDER WHERE STATUS_SEQ >= 1 AND (ORDER_DATE > DATEADD(M ,-3, GETDATE())) AND CARD_SEQ = MAX(SMC.CARD_SEQ)) AS OrderCount
		,	( DATEDIFF(DD, MAX(SMC.REG_DATE), GETDATE()) + 1 ) AS DateCnt

	FROM	S4_MD_CHOICE SMC
	JOIN	S2_CARD SC ON SMC.CARD_SEQ = SC.CARD_SEQ
	JOIN	S2_CARDSALESSITE SCSS ON SMC.CARD_SEQ = SCSS.CARD_SEQ
	LEFT 
	JOIN	S2_CARDDISCOUNT SCD ON SCD.MinCount = 400 AND SCSS.CARDDISCOUNT_SEQ = SCD.CARDDISCOUNT_SEQ
	LEFT 
	JOIN	S2_CARDIMAGE SCI 
	ON		SMC.CARD_SEQ = SCI.CARD_SEQ 
	AND		SCSS.COMPANY_SEQ = SCI.COMPANY_SEQ 
	AND		(
					(CARDIMAGE_WSIZE = '130' AND CARDIMAGE_HSIZE = '130') 
				OR	(CARDIMAGE_WSIZE = '180' AND CARDIMAGE_HSIZE = '180')
				OR	(CARDIMAGE_WSIZE = '210' AND CARDIMAGE_HSIZE = '210')
			)

	WHERE	1 = 1
	AND		SMC.MD_SEQ = @P_MD_SEQ
	AND		SCSS.COMPANY_SEQ = @P_COMPANY_SEQ
	-- AND		SCD.MINCOUNT = 400
 
	AND		(CASE WHEN @GRADE			= '' THEN '' ELSE SMC.GRADE			END = @GRADE)
	AND		(CASE WHEN @GUBUN			= '' THEN '' ELSE SMC.GUBUN			END = @GUBUN)
	AND		(CASE WHEN @P_BRAND_KIND	= '' THEN '' ELSE SMC.BRAND_KIND	END = @P_BRAND_KIND)

	AND		(CASE WHEN @CARD_BRAND		= '' THEN '' ELSE SC.CARDBRAND		END = @CARD_BRAND)

	GROUP BY SMC.MD_SEQ, SMC.SEQ
	ORDER BY MAX(SMC.SORTING_NUM) ASC

END


GO
