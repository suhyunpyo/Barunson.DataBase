IF OBJECT_ID (N'dbo.SP_EXEC_COUPON_ISSUE_FOR_ONE_BARUNSONCARD', N'P') IS NOT NULL DROP PROCEDURE dbo.SP_EXEC_COUPON_ISSUE_FOR_ONE_BARUNSONCARD
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*

EXEC [SP_EXEC_COUPON_ISSUE_FOR_ONE] 5006, 'SA', 's4guest', 'BCE2-49D1-4EFF-BE4C'
select * from coupon_issue where uid = 's4guest'

*/
/*

이 프로시저는 쿠폰의 최대 다운로드 수량에 대한 제어 때문에,
고객에게 정상적인 절차를 통해서 지급할때에만 사용해야 함.
관리자가 임의로 쿠폰을 고객에게 지급할때에는 다른 방식으로 지급 하길 권고함.

*/
CREATE PROCEDURE [dbo].[SP_EXEC_COUPON_ISSUE_FOR_ONE_BARUNSONCARD]
		@COMPANY_SEQ			AS INT
	,	@SALES_GUBUN			AS VARCHAR(50)
	,   @USER_ID				AS VARCHAR(50)
	,   @COUPON_CODE			AS VARCHAR(50)
AS
BEGIN
    
    SET NOCOUNT ON;

    DECLARE @COUPON_MST_SEQ		AS INT
		,	@RESULT_CODE		AS VARCHAR(4)	= '0000'
		,	@RESULT_MESSAGE		AS VARCHAR(500)	= ''

	DECLARE	@COUPON_ISSUE_CNT					AS INT
		,	@DOWNLOAD_MAX_QTY					AS INT
		,	@DOWNLOAD_LIMIT_PER_PERSON_QTY		AS INT
		,	@DOWNLOADED_CNT						AS INT
		,	@EXPIRY_TYPE						AS VARCHAR(1)
		,	@EXPIRY_START_DATE					AS DATETIME
		,	@EXPIRY_END_DATE					AS DATETIME
		,	@EXPIRY_CUSTOM_VALUE				AS INT
		,	@DOWNLOAD_USER_GB					AS VARCHAR(1)
		,	@REFERER_SALES_GUBUN				AS VARCHAR(50)
		,	@USER_REG_START_DATE				AS DATETIME
		,	@USER_REG_END_DATE					AS DATETIME
		,	@WEDDING_START_DATE					AS DATETIME
		,	@WEDDING_END_DATE					AS DATETIME
		,	@WEDD_PLACE							AS NVARCHAR(300)
		,	@SAMPLE_ORDER_TYPE					AS VARCHAR(10)
		,	@SAMPLE_ORDER_START_DATE			AS DATETIME
		,	@SAMPLE_ORDER_END_DATE				AS DATETIME
		,	@CARD_ORDER_TYPE					AS VARCHAR(10)
		,	@CARD_ORDER_CNT						AS INT
		,	@STATUS_TYPE_CODE					AS VARCHAR(6)
		,	@COUPON_ISSUE_START_DATE			AS DATETIME
		,	@COUPON_ISSUE_END_DATE				AS DATETIME
		,	@STATUS_ACTIVE_YN					AS VARCHAR(6)



	SELECT	@COUPON_MST_SEQ						= COUPON_MST_SEQ
		,	@COUPON_ISSUE_CNT					= COUPON_ISSUE_CNT
		,	@DOWNLOAD_MAX_QTY					= DOWNLOAD_MAX_QTY
		,	@DOWNLOAD_LIMIT_PER_PERSON_QTY		= DOWNLOAD_LIMIT_PER_PERSON_QTY			
		,	@DOWNLOADED_CNT						= DOWNLOADED_CNT				
		,	@EXPIRY_TYPE						= EXPIRY_TYPE			
		,	@EXPIRY_START_DATE					= EXPIRY_START_DATE		
		,	@EXPIRY_END_DATE					= EXPIRY_END_DATE		
		,	@EXPIRY_CUSTOM_VALUE				= EXPIRY_CUSTOM_VALUE	
		,	@DOWNLOAD_USER_GB					= DOWNLOAD_USER_GB		
		,	@REFERER_SALES_GUBUN				= REFERER_SALES_GUBUN	
		,	@USER_REG_START_DATE				= USER_REG_START_DATE	
		,	@USER_REG_END_DATE					= USER_REG_END_DATE		
		,	@WEDDING_START_DATE					= WEDDING_START_DATE		
		,	@WEDDING_END_DATE					= WEDDING_END_DATE		
		,	@WEDD_PLACE							= WEDD_PLACE					
		,	@SAMPLE_ORDER_TYPE					= SAMPLE_ORDER_TYPE		
		,	@SAMPLE_ORDER_START_DATE			= SAMPLE_ORDER_START_DATE
		,	@SAMPLE_ORDER_END_DATE				= SAMPLE_ORDER_END_DATE		
		,	@CARD_ORDER_TYPE					= CARD_ORDER_TYPE			
		,	@CARD_ORDER_CNT						= CARD_ORDER_CNT			
		,	@COUPON_ISSUE_START_DATE			= COUPON_ISSUE_START_DATE
		,	@COUPON_ISSUE_END_DATE				= COUPON_ISSUE_END_DATE	
		,	@STATUS_ACTIVE_YN					= STATUS_ACTIVE_YN

	FROM	COUPON_MST
	WHERE	COUPON_MST_SEQ = ( SELECT TOP 1 COUPON_MST_SEQ FROM COUPON_DETAIL WHERE COUPON_CODE = @COUPON_CODE )







	/* 발행기간 */
	IF ISNULL(@COUPON_ISSUE_START_DATE, GETDATE()) > GETDATE() OR ISNULL(@COUPON_ISSUE_END_DATE, GETDATE()) < GETDATE()
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '발행기간이 아닙니다.'

	END



	/* 대상 사이트 */
	IF NOT EXISTS(SELECT * FROM COUPON_APPLY_SITE WHERE COUPON_MST_SEQ = @COUPON_MST_SEQ AND COMPANY_SEQ = @COMPANY_SEQ)
	BEGIN

		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '대상 사이트가 아닙니다.'

	END



	/* 개인별 다운로드 개수 */
	IF @DOWNLOAD_LIMIT_PER_PERSON_QTY <=	(
												SELECT	ISNULL(COUNT(*), 0)
												FROM	COUPON_ISSUE	CI
												JOIN	COUPON_DETAIL	CD ON CI.COUPON_DETAIL_SEQ = CD.COUPON_DETAIL_SEQ
												WHERE	1 = 1
												AND		CD.COUPON_MST_SEQ = @COUPON_MST_SEQ
												AND		CI.UID = @USER_ID
												AND		CI.COMPANY_SEQ = @COMPANY_SEQ
											)
	BEGIN

		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '이미 다운로드 되었습니다.'

	END



	/* 가입 사이트 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	S2_USERINFO_THECARD 
			WHERE	1 = 1
			AND		UID = @USER_ID
			AND		ISNULL	(
								CASE 
										WHEN REFERER_SALES_GUBUN IS NOT NULL AND REFERER_SALES_GUBUN <> '' THEN REFERER_SALES_GUBUN
										ELSE SELECT_SALES_GUBUN
								END
							, 'SB')
					IN 
					( SELECT VALUE FROM FN_SPLIT(@REFERER_SALES_GUBUN, '|') )
		)
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '가입 사이트가 아닙니다.'

	END



	/* 가입일 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	S2_USERINFO_THECARD 
			WHERE	1 = 1
			AND		UID = @USER_ID
			AND		INTERGRATION_DATE >= ISNULL(@USER_REG_START_DATE, '2016-06-01 00:00:00')
			AND		INTERGRATION_DATE <= ISNULL(@USER_REG_END_DATE, GETDATE())
		)
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 발행 조건에 해당되지 않습니다. (가입일)'

	END



	/* 결혼예식일 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	S2_USERINFO_THECARD 
			WHERE	1 = 1
			AND		UID = @USER_ID
			AND		(
						CASE 
								WHEN ISDATE(WEDD_YEAR + '-' + WEDD_MONTH + '-' + WEDD_DAY) = 1 
								THEN WEDD_YEAR + '-' + WEDD_MONTH + '-' + WEDD_DAY
								ELSE '1900-01-01'
						END
						>= @WEDDING_START_DATE
						OR @WEDDING_START_DATE IS NULL
					)

			AND		(
						CASE 
								WHEN ISDATE(WEDD_YEAR + '-' + WEDD_MONTH + '-' + WEDD_DAY) = 1 
								THEN WEDD_YEAR + '-' + WEDD_MONTH + '-' + WEDD_DAY
								ELSE '2099-01-01'
						END
						<= @WEDDING_END_DATE
						OR @WEDDING_END_DATE IS NULL
					)
		)
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 발행 조건에 해당되지 않습니다. (결혼예식일)'

	END



	/* 샘플 주문 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	CUSTOM_SAMPLE_ORDER 
			WHERE	1 = 1
			AND		MEMBER_ID = @USER_ID
			AND		STATUS_SEQ >= 4
			AND		(REQUEST_DATE >= @SAMPLE_ORDER_START_DATE OR @SAMPLE_ORDER_START_DATE IS NULL)
			AND		(REQUEST_DATE <= @SAMPLE_ORDER_END_DATE OR @SAMPLE_ORDER_START_DATE IS NULL)
		)
		AND @SAMPLE_ORDER_TYPE = 'YES'
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 발행 조건에 해당되지 않습니다. (샘플주문)'

	END



	/* 청첩장 주문 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	CUSTOM_ORDER 
			WHERE	1 = 1
			AND		MEMBER_ID = @USER_ID
			AND		SETTLE_STATUS = 2
			AND		ORDER_COUNT > @CARD_ORDER_CNT
		)
		AND @CARD_ORDER_TYPE = 'YES'
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 발행 조건에 해당되지 않습니다. (청첩장주문)'

	END



	/* 예식 지역 */
	IF  NOT EXISTS
		(
			SELECT	*
			FROM	CUSTOM_ORDER CO
			JOIN	CUSTOM_ORDER_WEDDINFO COW ON CO.ORDER_SEQ = COW.ORDER_SEQ
			WHERE	1 = 1
			AND		CO.MEMBER_ID = @USER_ID
			AND		LEFT(ISNULL(COW.WEDD_ADDR, ''), 2) IN ( SELECT VALUE FROM FN_SPLIT(@WEDD_PLACE, '|') )			
		)
		AND ISNULL(@WEDD_PLACE, '') <> ''
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 발행 조건에 해당되지 않습니다. (예식 지역)'

	END



	/* 사용 가능 여부 COUPON_MST */
	IF @STATUS_ACTIVE_YN = 'N'
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '사용할 수 없는 쿠폰 입니다.'

	END



	/* 사용 가능 여부 COUPON_DETAIL */
	IF ISNULL((SELECT DOWNLOAD_ACTIVE_YN FROM COUPON_DETAIL WHERE COUPON_CODE = @COUPON_CODE), 'N') = 'N'
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '사용할 수 없는 쿠폰 입니다.'

	END



	/* 다운로드 수량 체크 */
	IF @DOWNLOAD_MAX_QTY > 0 AND @DOWNLOAD_MAX_QTY <= @DOWNLOADED_CNT
	BEGIN
		
		SET @RESULT_CODE	= '9999'
		SET @RESULT_MESSAGE = '쿠폰 수량이 모두 소진 되었습니다.'

	END



	/* 쿠폰 발급 */
	IF @RESULT_CODE = '0000'
	BEGIN
		
		DECLARE @COUPON_DETAIL_SEQ AS INT

		SELECT	TOP 1
				@COUPON_DETAIL_SEQ = COUPON_DETAIL_SEQ
		FROM	COUPON_DETAIL
		WHERE	1 = 1
		AND		COUPON_CODE = @COUPON_CODE
		AND		DOWNLOAD_ACTIVE_YN = 'Y'



		IF @COUPON_DETAIL_SEQ IS NULL
		BEGIN

			SET @RESULT_CODE	= '9999'
			SET @RESULT_MESSAGE = '사용할 수 없는 쿠폰 입니다.'

		END
		ELSE
		BEGIN

			INSERT INTO COUPON_ISSUE ( COUPON_DETAIL_SEQ, UID, ACTIVE_YN, COMPANY_SEQ, SALES_GUBUN, END_DATE, REG_DATE )

			SELECT	@COUPON_DETAIL_SEQ
				,	RTRIM(LTRIM(@USER_ID))
				,	'Y'
				,	@COMPANY_SEQ
				,	@SALES_GUBUN
				,	CASE WHEN @EXPIRY_TYPE = 'V' THEN (CONVERT(VARCHAR(10), DATEADD(DAY, @EXPIRY_CUSTOM_VALUE, GETDATE()), 120) + ' 23:59:59.997') ELSE NULL END
				,	GETDATE()

			SET @RESULT_CODE	= '0000'
			SET @RESULT_MESSAGE = '쿠폰이 발행되었습니다.'



			/* 쿠폰 다운로드 카운트를 증가 */
			UPDATE	COUPON_MST
			SET		DOWNLOADED_CNT = DOWNLOADED_CNT + 1
			WHERE	COUPON_MST_SEQ = @COUPON_MST_SEQ



			/* 1회성 쿠폰이면 발행된 쿠폰의 다운로드 여부를 비활성화 함 */
			/* 쿠폰이 1개만 생성되있을 경우에는 반복성 쿠폰 */
			/* 쿠폰이 여러개면 1회성 쿠폰 */
			IF (SELECT COUNT(*) FROM COUPON_DETAIL WHERE COUPON_MST_SEQ = @COUPON_MST_SEQ) > 1
			BEGIN

				UPDATE	COUPON_DETAIL
				SET		DOWNLOAD_ACTIVE_YN = 'N'
				WHERE	1 = 1
				AND		COUPON_MST_SEQ = @COUPON_MST_SEQ
				AND		COUPON_DETAIL_SEQ = @COUPON_DETAIL_SEQ

			END



		END

	END



    SELECT  @RESULT_CODE	AS RESULT_CODE
		,	@RESULT_MESSAGE AS RESULT_MESSAGE



END
GO
